; Pip-Boy Weapon Mod Vendor - Stentorious

; On game restart
if eval Goo1.AuxVarGetFlt "*PMV_Init"
	return
endif
Goo1.AuxVarSetFlt "*PMV_Init" 1

; Check for requirements
if eval GetNVSEVersion < 6 || (GetNVSEVersion == 6 && GetNVSERevision < 4)
   MessageBoxEx "Pip-Boy Weapon Mod Vendor missing requirement!%rInstall xNVSE 6.4.0+."
   return
endif
if GetPluginVersion "JohnnyGuitarNVSE" < 506
	MessageBoxEx "Pip-Boy Weapon Mod Vendor missing requirement!%rInstall JohnnyGuitar NVSE 5.06+."
	return
endif
if eval GetPluginVersion "ShowOffNVSE Plugin" < 145
	MessageBoxEx "Tactical Freelook missing requirement!%rInstall latest ShowOff NVSE plugin."
	return
endif
if GetPluginVersion "lStewieAl's Tweaks" < 905
	MessageBoxEx "Pip-Boy Weapon Mod Vendor missing requirement!%rInstall lStewieAl's Tweaks 9.05+."
	return
endif
if eval IsModLoaded "The Weapon Mod Menu.esp"
	MessageBoxEx "Pip-Boy Weapon Mod Vendor incompatibility detected!%rUninstall The Weapon Mod Menu."
	return
endif

; Load INI settings
Goo1.AuxVarSetFlt "*PMV_INI" (GetMaxOf 0 (GetINIFloat_Cached "General:fPriceMultiplier" "Stentorious\PipBoyModVendor.ini" 3)) 0
Goo1.AuxVarSetFlt "*PMV_INI" (GetINIFloat_Cached "General:iDeliveryMethod" "Stentorious\PipBoyModVendor.ini" 0) 1
Goo1.AuxVarSetFlt "*PMV_INI" (GetINIFloat_Cached "General:bBarterCalculation" "Stentorious\PipBoyModVendor.ini" 0) 2
CloseFileSO "Stentorious\PipBoyModVendor.ini" 0

; Inject Weapon Mod Vendor UI
InjectUIXML "ItemModMenu\GLOW_BRANCH\IMM_MainRect" "data\menus\prefabs\Stentorious\PipBoyModVendorCaps.xml"
SetUIFloatAlt "ItemModMenu\GLOW_BRANCH\IMM_MainRect\IMM_PlayerCapsInfo\x" (GetUIFloatAlt "InventoryMenu\GLOW_BRANCH\IM_MainRect\IM_HeadlineRect\IM_Headline_PlayerCapsInfo\x")
SetUIFloatAlt "ItemModMenu\GLOW_BRANCH\IMM_MainRect\IMM_PlayerCapsInfo\width" (GetUIFloatAlt "InventoryMenu\GLOW_BRANCH\IM_MainRect\IM_HeadlineRect\IM_Headline_PlayerCapsInfo\width")
SetUIFloatAlt "ItemModMenu\font" (GetUIFloatAlt "InventoryMenu\font")
if eval IsModLoaded "Vanilla UI Plus.esp"
	SetUIFloatAlt "ItemModMenu\GLOW_BRANCH\IMM_MainRect\IMM_PlayerCapsInfo\CI_TopLine\visible" 0
	SetUIFloatAlt "ItemModMenu\GLOW_BRANCH\IMM_MainRect\IMM_PlayerCapsInfo\CI_RightVert\visible" 0
endif

; Inject ySI caps icon
if eval GetINIFloat "ySI:bCapsIcon" "Stentorious\PipBoyUITweaks.ini" && GetPluginVersion "yUI" >= 140 && FileExists "menus\ySI\ySI.xml"
	InjectUIXML "ItemModMenu\GLOW_BRANCH\IMM_MainRect\IMM_PlayerCapsInfo" "data\menus\prefabs\Stentorious\ySICapIcon.xml"
	SetUIFloatAlt "ItemModMenu\GLOW_BRANCH\IMM_MainRect\IMM_PlayerCapsInfo\CI_TitleText\visible" 0
	if eval IsModLoaded "DarNifiedUINV.esp"
		ModUIFloat "ItemModMenu\GLOW_BRANCH\IMM_MainRect\IMM_PlayerCapsInfo\ySIImage\y" -1.5
	endif
endif

; Update caps display
SetOnMenuOpenEventHandler ({int iMenuID} => SetUIFloatAlt "ItemModMenu\GLOW_BRANCH\IMM_MainRect\IMM_PlayerCapsInfo\_value" (Player.GetItemCount Caps001)) 1 1061

; Init mod vendor
SetOnMenuClickEventHandler (CompileScript "PipBoyModVendor\MsgInit.gek") 1 "ItemModMenu\GLOW_BRANCH\IMM_MainRect\IMM_ItemModList\IMM_ItemModListTemplateRect"
